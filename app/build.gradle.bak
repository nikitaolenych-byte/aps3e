plugins {
    alias(libs.plugins.android.application)
}

def aps3eSd8sGen3 = project.hasProperty('aps3eSd8sGen3') && project.property('aps3eSd8sGen3').toString() == 'true'

def releaseStoreFile = (project.findProperty('RELEASE_STORE_FILE') ?: System.getenv('RELEASE_STORE_FILE'))
def releaseStorePassword = (project.findProperty('RELEASE_STORE_PASSWORD') ?: System.getenv('RELEASE_STORE_PASSWORD'))
def releaseKeyAlias = (project.findProperty('RELEASE_KEY_ALIAS') ?: System.getenv('RELEASE_KEY_ALIAS'))
def releaseKeyPassword = (project.findProperty('RELEASE_KEY_PASSWORD') ?: System.getenv('RELEASE_KEY_PASSWORD'))
def hasReleaseSigning = releaseStoreFile != null && releaseStorePassword != null && releaseKeyAlias != null && releaseKeyPassword != null

android {
    namespace 'aenu.aps3e'
    compileSdk 35

    defaultConfig {
        applicationId "aenu.aps3e"
        //applicationId "aenu.aps3e.premium"

        minSdk 28
        targetSdk 34
        versionCode 19
        versionName "1.19a"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                abiFilters 'arm64-v8a'
                // Enable with: ./gradlew :app:assembleRelease -Paps3eSd8sGen3=true
                arguments "-DAPS3E_SNAPDRAGON_8S_GEN3_OPT=${aps3eSd8sGen3 ? 'ON' : 'OFF'}"
                cppFlags ''
            }
        }

        sourceSets{
            main {
                //jniLibs.srcDirs = ['src/main/lib']
            }
        }
    }

    signingConfigs {
        release {
            if (hasReleaseSigning) {
                storeFile file(releaseStoreFile)
                storePassword releaseStorePassword
                keyAlias releaseKeyAlias
                keyPassword releaseKeyPassword
            }
        }
        debug {
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // If no real keystore is provided, sign with debug key so the APK is installable.
            signingConfig hasReleaseSigning ? signingConfigs.release : signingConfigs.debug
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }




    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }

    packaging{
        jniLibs.useLegacyPackaging = true
    }
}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.preference
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}